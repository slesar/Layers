group 'com.psliusar'

apply plugin: 'com.android.library'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'maven'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
    }
    buildTypes {
        release {

        }
    }
}

unMock {
    keep "android.os.Bundle"
    keep "android.os.Parcelable"
    keep "android.util.SparseArray"
    keep "com.android.internal.util.ArrayUtils"
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'

    // Test
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.98-beta'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../../maven'))
            pom {
                name = 'Layers name'
                artifactId = 'layers'
                packaging = 'aar'
                description = 'Description'
                //groupId = 'com.psliusar'
                //url = ''
                version = '1.0-SNAPSHOT'
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    androidJavadocs.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}
